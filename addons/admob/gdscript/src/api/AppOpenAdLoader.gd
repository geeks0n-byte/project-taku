
class_name AppOpenAdLoader
extends MobileSingletonPlugin

static var _plugin = _get_plugin("PoingGodotAdMobAppOpenAd")

var app_open_ad_load_callback: AppOpenAdLoadCallback
var _uid: int


func _init():
	if _plugin:
		_uid = _plugin.create()


func load(
	ad_unit_id: String,
	ad_request: AdRequest,
	app_open_ad_load_callback := AppOpenAdLoadCallback.new()
) -> void:
	if _plugin:
		self.app_open_ad_load_callback = app_open_ad_load_callback
		safe_connect(_plugin, "on_app_open_ad_loaded", _on_app_open_ad_loaded, CONNECT_DEFERRED)
		safe_connect(
			_plugin,
			"on_app_open_ad_failed_to_load",
			_on_app_open_ad_failed_to_load,
			CONNECT_DEFERRED
		)
		reference()
		_plugin.load(ad_unit_id, ad_request.convert_to_dictionary(), ad_request.keywords, _uid)


func _on_app_open_ad_loaded(uid: int) -> void:
	if uid == _uid:
		app_open_ad_load_callback.on_ad_loaded.call(AppOpenAd.new(uid))
		unreference.call_deferred()


func _on_app_open_ad_failed_to_load(uid: int, load_ad_error_dictionary: Dictionary) -> void:
	if uid == _uid:
		app_open_ad_load_callback.on_ad_failed_to_load.call(
			LoadAdError.create(load_ad_error_dictionary)
		)
		unreference.call_deferred()
